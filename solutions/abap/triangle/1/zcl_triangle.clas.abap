CLASS zcl_triangle DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC.

  PUBLIC SECTION.
    METHODS:
      is_equilateral
        IMPORTING
          side_a             TYPE f
          side_b             TYPE f
          side_c             TYPE f
        RETURNING
          VALUE(result)      TYPE abap_bool
        RAISING
          cx_parameter_invalid,
      is_isosceles
        IMPORTING
          side_a             TYPE f
          side_b             TYPE f
          side_c             TYPE f
        RETURNING
          VALUE(result)      TYPE abap_bool
        RAISING
          cx_parameter_invalid,
      is_scalene
        IMPORTING
          side_a             TYPE f
          side_b             TYPE f
          side_c             TYPE f
        RETURNING
          VALUE(result)      TYPE abap_bool
        RAISING
          cx_parameter_invalid.

  PROTECTED SECTION.
  PRIVATE SECTION.

    methods validate_triangle
      IMPORTING
          side_a             TYPE f
          side_b             TYPE f
          side_c             TYPE f
        RAISING
          cx_parameter_invalid.

ENDCLASS.


CLASS zcl_triangle IMPLEMENTATION.

  METHOD is_equilateral.
    validate_triangle( side_a = side_a side_b = side_b side_c = side_c ). 
    
    if side_a = side_b and side_b = side_c.
      result = abap_true.
    endif.
  ENDMETHOD.

  METHOD is_isosceles.
    validate_triangle( side_a = side_a side_b = side_b side_c = side_c ).

    if side_a = side_b or side_b = side_c or side_a = side_c.
      result = abap_true.
    endif.
  ENDMETHOD.

  METHOD is_scalene.
    validate_triangle( side_a = side_a side_b = side_b side_c = side_c ).

    IF side_a <> side_b AND side_b <> side_c AND side_a <> side_c.
      result = abap_true.
    ENDIF.
  ENDMETHOD.

  METHOD validate_triangle.
    IF side_a <= 0 OR side_b <= 0 OR side_c <= 0.
      RAISE EXCEPTION TYPE cx_parameter_invalid.
    ENDIF.

    IF ( side_a + side_b < side_c ) OR
       ( side_a + side_c < side_b ) OR
       ( side_b + side_c < side_a ).
      RAISE EXCEPTION TYPE cx_parameter_invalid.
    ENDIF.
  ENDMETHOD.

ENDCLASS.
